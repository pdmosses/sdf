module generation/docs/init-website

imports
    libspoofax/core/-
    libspoofax/resource/-

rules // initialise-website

//  For each directory PROJECT/DIR, generate the configuration file 
//  REPO/docs/LANG/DIR/.pages for use by mkdocs-awesome-pages-plugin.
//  This stops MkDocs transforming DIR names such as `aliases` to `Aliases`.
//  (If the plugin had an option to suppress the MkDocs transformation,
//  these files would not be needed.)

//  initialise-website: _ -> None()

  initialise-website =
      current-language-source-files
    ; map(dirname; string-replace(|$[[<project-path>]/], ""); gen-docs-dir)
    ; !None()

  gen-docs-dir: dir -> ()
    with
      repo-clone := <project-path; local-path; dirname>
    ; pages-file := $[[repo-clone]/docs/[<language>]/[dir]/.pages]
    ; pages-contents := $[title: [<base-filename> dir]]
    ; outs := <fopen> (pages-file, "w")
    ; <fputs> (pages-contents, outs)
    ; <fclose> outs
    ; if <is-substring(!"/")> dir
      then <dirname; gen-docs-dir> dir
      end

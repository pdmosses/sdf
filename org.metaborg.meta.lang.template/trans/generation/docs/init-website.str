module generation/docs/init-website

imports
    libspoofax/core/-
    libspoofax/resource/-
    generation/docs/traverse

rules // initialise-website

//  For each directory PROJECT/syntax/DIR, generate the configuration file 
//  REPO/docs/LANG/syntax/DIR/.pages for use by the plugin mkdocs-awesome-pages
//  (to avoid MkDocs transforming DIR names such as `aliases` to `Aliases`).
//  If the plugin had an option to suppress transformation, these configuration
//  files would not need to be generated.

  initialise-website: (_, _, ast, path, project-path) -> ()
    with <gen-syntax-docs-dirs> project-path

  gen-syntax-docs-dirs: project-path -> ()
    with <traverse-dirs(gen-docs-dir(|project-path))> $[[project-path]/syntax]

  gen-docs-dir(|project-path): dir -> ()
    with repo-clone := <local-path; dirname> project-path
    ; pages-file := $[[repo-clone]/docs/[<language>]/syntax/[dir]/.pages]
    ; pages-contents := $[title: [dir]]
    ; outs := <fopen> (pages-file, "w")
    ; <fputs> (pages-contents, outs)
    ; <fclose> outs
